(ns acme.view
  (:require ["package:flutter/material.dart" :as m]
            [cljd.flutter :as f]
            [acme.model :as model]
            [hti.re-dash.core :as rd]))

(def description
  (f/widget
    (m/Container
      .padding (m/EdgeInsets.all 10)
      .margin (m/EdgeInsets.all 20)
      .decoration (m/BoxDecoration
                    .border (m/Border.all .width 1)
                    .borderRadius (m/BorderRadius.all (m/Radius.circular 20))))
    (m/Row
      .mainAxisAlignment m/MainAxisAlignment.center
      .crossAxisAlignment m/CrossAxisAlignment.center
      .children
      [(m/Icon m/Icons.info)
       (m/SizedBox .width 10)
       (m/Flexible
         .child (m/Text "Shows an example of ordered event execution during longer processing events & effects. Logged to console."))])))


(def counter
  (f/widget
    :watch [current-count (rd/subscribe [::model/get-count])]
    m/Expanded
    (m/Column
      .mainAxisAlignment m/MainAxisAlignment.center
      .children
      [(m/Text (str current-count))
       (m/IconButton
         .icon (m/Icon (m/Icons.add_circle_outline))
         .onPressed #(do
                       (rd/dispatch [::model/count-one])
                       (rd/dispatch [::model/count-three])
                       (rd/dispatch [::model/count-one])
                       (rd/dispatch [::model/count-three])
                       (rd/dispatch [::model/count-one])
                       (rd/dispatch [::model/count-one])
                       (println "Multiple events dispatched!")))
       (m/Text "Click me! I'll count multiple times.")])))

(def view
  (m/Column
    .mainAxisAlignment m/MainAxisAlignment.center
    .children
    [description
     counter]))
